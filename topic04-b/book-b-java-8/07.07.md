#Lambdas & Android (Gradle configuration)

We are presently developing our Android apps in a Java 7 environment with `targetSdkVersion` set to 23 (Marshmallow). Version 24, Nougat, released in August 2016, introduces a new experimental tool chain called [`Jack and Jill`](http://tools.android.com/tech-docs/jackandjill) that provides support for a subset of Java 8 features. 

- Java Android Compiler Kit (Jack)
- Java Intermediate Library Linker (Jill)

There is, however, a plugin called `Retrolambda` that provides a backport of lambda syntax to our environment - Java 7 and version 23. We shall use this in this lab rather the Jack compiler to test the use of lambdas.

##Gradle files

Here we shall configure both gradle files to import the `retrolamda` plugin.

Create and checkout a new branch named `lambda` at MyRent tag 05 using the following command:

```
git checkout -b lambda 00
```
Open the top-level build.gradle file.

In the buildscript block add the following snippets in the locations shown:

```
buildscript {

  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }

    ...
    ...
  }

  dependencies {
    ...
    classpath 'me.tatarka:gradle-retrolambda:3.3.0'
    ...
  }
}
```

Here is the complete top level file:

```
// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }

    jcenter()

  }

  dependencies {
    classpath 'com.android.tools.build:gradle:2.1.3'
    classpath 'me.tatarka:gradle-retrolambda:3.3.0'
    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

allprojects {
  repositories {
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
```

In the module gradle file add the following snippets:

At the head of the file:

```
apply plugin: 'me.tatarka.retrolambda'
```

At the end of the `android` block, before its closing brace:
```
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
```
Here is the complete file:
```
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    applicationId "org.wit.myrent"
    minSdkVersion 19
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'
  compile 'com.android.support:appcompat-v7:23.4.0'

}

```

